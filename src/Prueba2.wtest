import Personaje.*
import artefactos.*
import eclipse.*

describe "Para los siguientes tests, Rolando debe tener como artefactos 
	la espada del destino, el collar divino y la m√°scara oscura.
	El collar divino debe tener 5 perlas." {
	
	var espadaDelDestino = new Arma()
	var mascaraOscura = new Mascara(1)
	var rolando = new Personaje("",[espadaDelDestino, collarDivino, mascaraOscura],1)
	var mascaraParaNadaOscura = new Mascara(0)
	
	test "Al eliminar todos los artefactos de Rolando, su habilidad para 
		la lucha es 1(el valor base)" {
		rolando.artefactos().clear()
		assert.equals(1, rolando.habilidadLucha())
	}

	test "La habilidad para la lucha de Rolando con todos los artefactos es 13" {
		assert.equals(13, rolando.habilidadLucha())
	}

	test "Al cambiar la cantidad de perlas del collar divino a 3, la habilidad 
		para la lucha de Rolando con todos los artefactos es 11"{
		collarDivino.cantidadPerlas(3)
		assert.equals(11, rolando.habilidadLucha())	
	}

	test "Al cambiar el valor base para la lucha de Rolando a 8, su habilidad
		para la lucha con todos los artefactos es 20" {
		rolando.valorBaseLucha(8)
		assert.equals(20, rolando.habilidadLucha())
	}

	test "Al representar un eclipse, el valor de lucha de la mascara oscura es 5" {
		eclipse.representarEclipse()
		assert.equals(5, mascaraOscura.unidadesDeLucha())
	}

	test "Al eliminar la mascara oscura de los artefactos de Rolando, su habilidad
		para la lucha es 9" {
		rolando.removerArtefacto(mascaraOscura)
		assert.equals(9, rolando.habilidadLucha())	
	}
	
	test "Si tenemos una mascara oscura con indice de oscuridad de 0, las
	unidades de lucha que aporta es 4 "{
		assert.equals(4, mascaraParaNadaOscura.unidadesDeLucha())
	}
	
	test "Si tenemos una mascara oscura con indice de oscuridad de 0 y 
	con minimo de poder en 2, las unidades de lucha que aporta es 2 "{
		mascaraParaNadaOscura.minimoDePoder(2)
		assert.equals(2, mascaraParaNadaOscura.unidadesDeLucha())
	}
}